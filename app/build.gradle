// IMPORTANT:
// -> To build directly using prebuilt libraries (*.so), comment the "externalNativeBuild" blocks
// from below, and make sure the libs are inside (automatically copied after building from source):
// /project-root/app/src/debug/jniLibs (for a debug build)
// /project-root/app/src/main/jniLibs (for a release build)
// - > To build from source, just make sure the folders above exist and uncomment the
// "externalNativeBuild" blocks below.

apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(file('../keystore.properties')))

android {
    signingConfigs {
        debug {
            storeFile file('key.jks')
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
        release {
            storeFile file('key.jks')
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdk 33
    defaultConfig {
        applicationId 'com.example.aliayubkhan.LSLReceiver'
        minSdkVersion 26
        versionCode 1       // increment with every release
        versionName '1.16.2'   // change with every release
        setProperty('archivesBaseName', "LSLRecorder-$versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your app.
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild.cmake {
            arguments "-DANDROID_CPP_FEATURES=rtti exceptions", "-DLSL_NO_FANCY_LIBNAME=1", "-DANDROID_PLATFORM=24"
            targets "lsl", "generate_xdf"
        }
    }
    externalNativeBuild.cmake {
        path "../liblsl/CMakeLists.txt"
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        jniLibs {
            pickFirsts += ['**/lib/**']
        }
        resources {
            pickFirsts += ['**/lib/**']
        }
    }
    buildToolsVersion = '34.0.0'
    ndkVersion '25.2.9519653'
    namespace 'com.example.aliayubkhan.LSLReceiver'
}

dependencies {
    implementation fileTree(dir: '../libs', include: ['*.jar'])
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'net.java.dev.jna:jna:5.13.0@aar'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'junit:junit:4.13.2'
}

// copy native libraries to per project location
tasks.register('copyDebugJniLibs', Copy) {
    from 'build/intermediates/cmake/debug/obj'
    include('**/*.*')
    into 'src/debug/jniLibs'
}
tasks.register('copyReleaseJniLibs', Copy) {
    from 'build/intermediates/cmake/release/obj'
    include('**/*.*')
    into 'src/main/jniLibs'
}

tasks.configureEach { task ->
    if (task.name == 'assembleDebug') task.finalizedBy('copyDebugJniLibs')
    if (task.name == 'assembleRelease') task.finalizedBy('copyReleaseJniLibs')
}

